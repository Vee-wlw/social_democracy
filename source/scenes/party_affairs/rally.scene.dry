title: Wahlkundgebung
new-page: true
is-card: true
tags: party_affairs
view-if: rally_timer <= 0
on-arrival: month_actions += 1; rally_timer = 6
card-image: img/protest.jpg
go-to: main_rally; sa_disrupt if sa_strength > 180 and not sa_banned and not return_to_normalcy

@sa_disrupt

Die SA versucht unseren Wahlkampf zu stören. Ihre Straßenkämpfer haben unsere Kundgebung angegriffen.

- @police_protect: Ruft die Polizei zur hilfe. Call the police to protect our rally.
- @rb_protect: Der Reichsbanner könnte die Kundgebung beschützen.
- @both_protect: Die Polizei und der Reichsbanner könnten zusammenarbeiten um die Kundgebung zu schützen?
- @cancel: Löst die Kundgebung auf.

@police_protect
view-if: spd_prussia == 1
on-arrival: police_protect_success = prussian_police_loyalty * prussian_police_militancy * prussian_police_strength*2 - sa_militancy*sa_strength;
go-to: police_protect_win if police_protect_success >= 0; police_protect_lose if police_protect_success < 0

@police_protect_win
go-to: main_rally

Die Polizei hat unsere Kundgebung beschützt. Unser Treffen kann stattfinden.
@police_protect_lose
on-arrival: strife += 1; workers_nsdap += 3

Leider konnte oder wollte die Polizei die SA nicht zurückhalten. Die Nazis haben diesmal gewonnen.

@rb_protect
on-arrival: rb_success = rb_strength*rb_militancy - sa_strength*sa_militancy; strife += 1
go-to: rb_protect_win if rb_success >= 0; rb_protect_lose if rb_success < 0

@rb_protect_win
go-to: main_rally

Der Reichsbanner konnte unsere Kundgebung vor der SA beschützen! Unsere Kundgebung kann stattfinden.

@rb_protect_lose
on-arrival: workers_nsdap += 3; rb_strength -= 50

Leider konnte der Reichsbanner die SA nicht zurückhalten. Die Nazis haben diesmal gewonnen.

@both_protect
view-if: spd_prussia
choose-if: prussian_police_training > 0
unavailable-subtitle: Die treuen Teile der Polizei haben noch nie mit dem Reichsbanner zusammengearbeitet.
on-arrival: rb_success = rb_strength*rb_militancy - sa_strength*sa_militancy + prussian_police_loyalty*prussian_police_militancy*prussian_police_strength*2; strife += 1
go-to: both_protect_win if rb_success >= 0; both_protect_lose if rb_success < 0

@both_protect_win
go-to: main_rally

Die Polizei und der Reichsbanner konnten unsere Kundgebung vor der SA beschützen. Unser Treffen kann stattfinden.

@both_protect_lose
on-arrival: workers_nsdap += 3; rb_strength -= 50

Leider konnte die Polizei und der Reichsbanner die SA nicht zurückhalten. Die Nazis haben diesmal gewonnen.


@cancel
on-arrival: workers_nsdap += 3;

Die Nazis haben diesmal gewonnen.

@main_rally

 Mass rallies are a common feature of social democratic life, especially in the lead-up to an election. What is the primary theme of this rally?

- @class_struggle: The class struggle of the proletariat against the bourgeoisie.
- @welfare: Sozialpolitik - the maintenance and expansion of social welfare programs.
- @democracy: Expanding our nascent democracy and defending it against dictatorship and fascism.
- @pacifism: Opposing war, nationalism, and militarism at all costs.
- @wtb: Support the WTB plan!
- @local: A mixture of various local and national concerns that resonate most with the audience.
- @easy_discard

@class_struggle
on-arrival: workers_spd += 4*(1-dissent); unemployed_spd += 3*(1-dissent) if left_strength > reformist_strength; workers_spd += 4*(1-dissent) if nationalization_progress > 0; unemployed_spd += 3*(1-dissent) if nationalization_progress > 0; left_strength += 5; left_dissent -= 5; old_middle_spd -= 3; new_middle_spd -= 2;

Our message of class struggle appeals to the left wing of the working class, while potentially alienating the middle class. [? if nationalization_progress > 0 : Our achievements in nationalization have assisted our messaging. ?]

@welfare
on-arrival: workers_spd += 3*(1-dissent); workers_spd += 3*(1-dissent) if welfare > 0; unemployed_spd += 3*(1-dissent) if welfare > 0; unemployed_spd += 4*(1-dissent) if welfare > 1; reformist_strength += 3; labor_strength += 4; reformist_dissent -= 3; labor_dissent -= 3;

The reformist message appeals to the organized workers, whose benefits are the accomplishments of social democracy. [? if welfare > 1 : Our achievements in expanding welfare have made this message more effective. ?]

@democracy
on-arrival: democratization += 1; pro_republic += 2*(1 - dissent); workers_spd += 4*(1-dissent) if democratization >= 3; unemployed_spd += 3*(1-dissent) if democratization >= 3; pro_republic += 4*(1 - dissent) if democratization >= 3; new_middle_spd += 4*(1-dissent) if democratization >= 3 

Our speakers defend the republican system and rail against the monarchists and authoritarians who threaten it.

[? if democratization < 3 : Talk of democracy does not excite the workers as much as more practical issues. But that may change; part of our job is to provide political education to the proletariat. ?]
[? if democratization >= 3 : Since we have consistently made democratization a theme of our campaign, the workers are enthusiastic about our message. ?]

@pacifism
on-arrival: pacifism += 1; nationalism -= 2*(1 - dissent); workers_spd += 4*(1 - dissent) if pacifism >= 3; unemployed_spd += 3*(1-dissent) if pacifism >= 3; nationalism -= 3*(1-dissent) if pacifism >= 3; new_middle_spd += 4*(1-dissent) if pacifism >= 3

Our speakers denounce the military establishment and all the spending entering that black hole. Some adventurous speakers question the responsibility of the military elites in organizing the Great War.

[? if pacifism < 3 : Given the volume of nationalist indoctrination in our society, it is difficult to argue so vehemently against the military. ?]
[? if pacifism >= 3 : Since we have consistently made pacifism a theme of our campaign, the workers are enthusiastic about our message. ?]

@wtb
view-if: wtb_adopted = 1
choose-if: return_to_normalcy = 0
unavailable-subtitle: The economic crisis is largely over now, and the public is no longer interested in hearing about it.
on-arrival: wtb_support += 1; workers_spd += 6*(1-dissent); unemployed_spd += 6*(1-dissent); rural_spd += 3*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 3*(1-dissent);

The WTB plan has become a popular rallying cry for our party. The workers are eager to go back to work, and that is exactly what the plan promises.


@local
on-arrival: workers_spd += 3*(1-dissent); old_middle_spd += 1*(1-dissent); new_middle_spd += 2*(1-dissent); unemployed_spd += 3*(1-dissent);

Our speakers have gauged the mood and are speaking to the issues that seem to most affect the workers in this moment.
